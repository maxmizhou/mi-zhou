buildscript {
    //版本管理
    ext {
        versions = [
                springBoot       : '3.0.0',
                springCloud      : '2022.0.1',
                alibabaCloud     : '2022.0.0.0-RC1',
                lombok           : '1.18.26',
                mysql            : '8.0.13',
                mybatisPlus      : '3.5.3.1',
                robot            : '0.0.39',
                lang3            : '3.12.0',
                commonio         : '2.11.0',
                jsonpath         : '2.8.0',
                fastjson         : '2.0.28',
                httpclient5      : '5.1.3',
                dynamicDatasource: '3.6.1',
                druidStarter     : '1.2.16',
                swagger          : "1.6.9",
                swagger3x        : "2.2.8"

        ]
    }
    //仓库管理 给插件用的
    repositories {
        //本地仓库
        mavenLocal()
        //阿里云仓库
        maven { url 'https://maven.aliyun.com/repository/public' }
        //中央仓库
        mavenCentral()
    }
    //插件管理
    dependencies {
        //spring-boot-gradle-plugin
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${versions.springBoot}")
    }
}
//全局管理
allprojects {
    apply plugin: 'java'
    group = 'com.mi.zhou'
    version = '1.0-SNAPSHOT'
    sourceCompatibility = 17
    targetCompatibility = 17
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'UTF-8'
        }
    }
    //仓库管理
    repositories {
        //本地仓库
        mavenLocal()
        //阿里云仓库
        maven { url 'https://maven.aliyun.com/repository/public' }
        //中央仓库
        mavenCentral()
    }
}
//子项目配置
subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    dependencies {
        annotationProcessor "org.projectlombok:lombok"
        compileOnly "org.projectlombok:lombok"
    }
    clean {
        delete 'build'
        delete 'out'
        delete 'logs'
    }
    dependencyManagement {
        dependencies {
            /*lombok*/
            dependency "org.projectlombok:lombok:${versions.lombok}"
            /*mybatis-plugs*/
            dependency "com.baomidou:mybatis-plus-boot-starter:${versions.mybatisPlus}"
            /*mysql*/
            dependency "mysql:mysql-connector-java:${versions.mysql}"
            /*QQRobot*/
            dependency "net.lz1998:pbbot-spring-boot-starter:${versions.robot}"
            //lang3
            dependency "org.apache.commons:commons-lang3:${versions.lang3}"
            //common-io
            dependency "commons-io:commons-io:${versions.commonio}"
            //json-path
            dependency "com.jayway.jsonpath:json-path:${versions.jsonpath}"
            //fastjson
            dependency "com.alibaba.fastjson2:fastjson2:${versions.fastjson}"
            //httpclient5-fluent
            dependency "org.apache.httpcomponents.client5:httpclient5-fluent:${versions.httpclient5}"
            //dynamic-datasource
            dependency "com.baomidou:dynamic-datasource-spring-boot-starter:${versions.dynamicDatasource}"
            //druid
            dependency "com.alibaba:druid-spring-boot-starter:${versions.druidStarter}"
            dependency "io.swagger:swagger-annotations:${versions.swagger}"
            dependency "io.swagger.core.v3:swagger-annotations:${versions.swagger3x}"
            dependency "io.swagger.core.v3:swagger-annotations-jakarta:${versions.swagger3x}"
        }
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${versions.springBoot}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${versions.springCloud}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${versions.alibabaCloud}"
        }
    }
}

